plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}
android {
    compileSdkVersion 30
    buildToolsVersion = '30.0.3'
    defaultConfig {
        applicationId "com.yujing.chuankou"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 21
        versionName "2.2.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        //bugly
        ndk{
            moduleName "app"
            abiFilters "armeabi-v7a"
        }
    }
    //签名
    signingConfigs {
        debug {
            storeFile file("doc/yujingtest.jks")
            storePassword "123456"
            keyAlias "test"
            keyPassword "123456"
        }
        release {
            storeFile file("doc/yujingtest.jks")
            storePassword "123456"
            keyAlias "test"
            keyPassword "123456"
        }
    }
    buildTypes {
        debug {
            //app名称
            manifestPlaceholders = ["app_name": "@string/app_name_debug"]
            //包名后缀
            //applicationIdSuffix ".debug"
            //版本名后缀
            //versionNameSuffix "-debug"
            //混淆
            minifyEnabled false
            //Zipalign优化
            zipAlignEnabled false
            // 移除无用的resource文件
            shrinkResources false
            //签名配置
            signingConfig signingConfigs.debug
        }
        release {
            //app名称
            manifestPlaceholders = ["app_name": "@string/app_name"]
            //混淆
            minifyEnabled false
            //Zipalign优化
            zipAlignEnabled false
            //移除无用的resource文件
            shrinkResources false
            //签名配置
            signingConfig signingConfigs.release
            //前一部分代表系统默认的android程序的混淆文件，该文件已经包含了基本的混淆声明，后一个文件是自己的定义混淆文件
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    //打包
    android.applicationVariants.all { variant ->
        //def appName = variant.productFlavors[0].name //获取渠道名称
        def appName = "YSerialPort" //rootProject.name
        def createDate = new Date().format("YYYYMMdd", TimeZone.getTimeZone("GMT+08:00"))
        def createTime = new Date().format("HHmmss", TimeZone.getTimeZone("GMT+08:00"))
        variant.outputs.all {
            it.outputFileName = "${appName}_v${versionName}_${versionCode}_${createDate}_${createTime}_${buildType.name}.apk"
        }
    }
    //资源合并
    sourceSets {
        main { res.srcDirs = ['src/main/res', 'src/main/res_screen'] }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        dataBinding = true
    }
    //支持AAR
    repositories {
        flatDir {
            dirs 'libs'
        }
    }
}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation fileTree(dir: 'libs', include: ['*.aar'])
    
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.4.0-alpha03'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    implementation 'com.kotlinx:yutils:1.7.1'
    implementation 'com.kotlinx:yhttp:1.1.0'

    //implementation 'com.kotlinx:yserialport:2.2.0'
    implementation project(':yserialport')

    //bugly，异常采集
    implementation 'com.tencent.bugly:crashreport:3.4.4'
    implementation 'com.tencent.bugly:nativecrashreport:3.9.2'

    //gson  <!-- https://mvnrepository.com/artifact/com.google.code.gson/gson -->
    implementation 'com.google.code.gson:gson:2.8.8'

    //____________________________________________________________RxJava____________________________________________________________
    //rxjava2，https://mvnrepository.com/artifact/io.reactivex.rxjava2/rxjava
    implementation group: 'io.reactivex.rxjava2', name: 'rxjava', version: '2.2.21'
    //rxandroid，https://mvnrepository.com/artifact/io.reactivex.rxjava2/rxandroid
    implementation group: 'io.reactivex.rxjava2', name: 'rxandroid', version: '2.1.1'
}
